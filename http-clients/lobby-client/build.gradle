plugins {
    id("maven-publish")
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

dependencies {
    implementation "io.github.openfeign:feign-core:$feignCoreVersion"
    implementation project(":game-app:domain-data")
    implementation project(":lib:feign-common")
    implementation project(":lib:java-extras")
    implementation project(":lib:websocket-client")
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonDataTypeVersion"
    testImplementation "com.google.code.gson:gson:$gsonVersion"
    testImplementation "ru.lanwen.wiremock:wiremock-junit5:$wireMockJunit5Version"
    testImplementation "org.apache.httpcomponents:httpclient:$apacheHttpComponentsVersion"
    testImplementation project(":lib:test-common")
}


def getGitCommitCount = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-list", "--count", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/triplea-game/triplea"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "org.triplea"
            artifactId = "http.client.lobby"
            version = "0." + getGitCommitCount()
            from(components.java)
            artifact(tasks["shadowJar"])
        }
    }
}
