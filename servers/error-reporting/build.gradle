plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

archivesBaseName = "$group-$name"
mainClassName = 'org.triplea.server.error.reporting.ErrorReportingServer'
ext {
    releasesDir = file("$buildDir/releases")
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from file('configuration.yml')

    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

shadowJar {
    archiveClassifier.set ''
    // mergeServiceFiles is needed by dropwizard
    // Without this configuration parsing breaks and is unable to find connector type 'http' for
    // the following YAML snippet:  server: {applicationConnectors: [{type: http, port: 8080}]
    mergeServiceFiles()
}

configurations {
    testImplementation {
        // database-rider brings in slf4j-simple as a transitive dependency
        // DropWizard has logback baked in and cannot have multiple slf4j bindings.
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
}


dependencies {
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation project(':http-clients:error-reporting-client')
    implementation project(':http-clients:github-client')
    implementation project(':http-clients:http-client-lib')
    implementation project(':servers:server-lib')

    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"

    testImplementation "com.github.database-rider:rider-junit5:$databaseRiderVersion"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation project(':servers:server-test-support')
    testImplementation project(':spitfire-server:database-test-support')
}
