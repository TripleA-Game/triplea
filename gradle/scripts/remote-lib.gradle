import java.nio.file.Paths

import de.undercouch.gradle.tasks.download.DownloadAction

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '3.4.3'
    }
}

def remoteLibsDir = file('.remote-libs')

ext.remoteLib = { url ->
    def file = file("$remoteLibsDir/${Paths.get(new URI(url).path).fileName}")
    def download = new DownloadAction(project)
    download.src url
    download.dest file
    download.overwrite false
    download.execute()
    files(file)
}

task cleanRemoteLibs(
        type: Delete,
        group: LifecycleBasePlugin.BUILD_GROUP,
        description: 'Deletes the remote libraries directory.') {
    delete remoteLibsDir
}
