import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'io.franzbecker.gradle-lombok' version '2.0' apply false
    id 'net.ltgt.errorprone' version '0.8.1' apply false
    id 'com.diffplug.gradle.spotless' version '3.23.0' apply false
}

apply from: 'gradle/scripts/yaml.gradle'

description = 'TripleA is a free online turn based strategy game and board game engine, similar to such board games as Axis & Allies or Risk.'

ext {
    schemasDir = file('config/triplea/schemas')
}

check {
    dependsOn 'validateYamls'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

task validateYamls(group: 'verification', description: 'Validates YAML files.') {
    doLast {
        def lobbyServerYamlFile = file('lobby_server.yaml')
        validateYaml(lobbyServerYamlFile, file("$schemasDir/lobby_server.json"))

        def mapsYamlFile = file('triplea_maps.yaml')
        validateYaml(mapsYamlFile, file("$schemasDir/triplea_maps.json"))
        validateMapsYamlUris(mapsYamlFile)
    }
}

allprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        groovyGradle {
            target '*.gradle', 'gradle/**/*.gradle'

            endWithNewline()
            indentWithSpaces()
            trimTrailingWhitespace()
        }
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'io.franzbecker.gradle-lombok'

    apply from: rootProject.file('gradle/scripts/release.gradle')
    apply from: rootProject.file('gradle/scripts/version.gradle')

    group = 'triplea'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    version = getProductVersion()

    ext {
        awaitilityVersion = '3.1.6'
        commonsCliVersion = '1.4'
        commonsCodecVersion = '1.11'
        commonsIoVersion = '2.6'
        commonsMathVersion = '3.6.1'
        databaseRiderVersion = '1.5.2'
        dropWizardTestingVersion = '1.3.12'
        dropwizardVersion = '1.3.15'
        equalsVerifierVersion = '3.1.4'
        errorProneVersion = '2.3.3'
        guavaVersion = "27.0.1-jre"
        guavaVersion = '27.0.1-jre'
        hamcrestJsonVersion = '0.2'
        hamcrestOptionalVersion = '2.0.0'
        hamcrestVersion = '2.0.0.0'
        jakartaMailVersion = '1.6.4'
        javafxVersion = '11'
        javaxActivationVersion = '1.1.1'
        jaxbApiVersion = '2.3.1'
        jaxbCoreVersion = '2.3.0'
        jaxbImplVersion = '2.3.2'
        jbcryptVersion = '0.4'
        jdbiVersion = '3.8.2'
        jlayerVersion = '1.0.1.4'
        junitJupiterVersion = '5.4.0'
        junitPioneerVersion = '0.3.0'
        junitPlatformLauncherVersion = '1.4.0'
        mockitoVersion = '2.24.5'
        openFeignVersion = '10.2.0'
        postgresqlVersion = '42.2.5'
        rateLimitjVersion = '0.6.0'
        slf4jSimpleVersion = '1.7.21'
        snakeYamlVersion = '1.0'
        sonatypeGoodiesPrefsVersion = '2.2.5'
        substanceVersion = '2.0.1'
    }

    repositories {
        jcenter()
    }

    dependencies {
        errorprone "com.google.errorprone:error_prone_core:$errorProneVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        testImplementation "com.github.npathai:hamcrest-optional:$hamcrestOptionalVersion"
        testImplementation "nl.jqno.equalsverifier:equalsverifier:$equalsVerifierVersion"
        testImplementation "org.hamcrest:java-hamcrest:$hamcrestVersion"
        testImplementation "org.junit-pioneer:junit-pioneer:$junitPioneerVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformLauncherVersion"
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += [
            '-Xlint:all,-processing',
            '-Xmaxwarns', '1000'
        ]
        options.encoding = 'UTF-8'
        options.errorprone {
            check 'ByteBufferBackingArray', CheckSeverity.ERROR
            check 'CatchAndPrintStackTrace', CheckSeverity.ERROR
            check 'ClassCanBeStatic', CheckSeverity.ERROR
            check 'DefaultCharset', CheckSeverity.ERROR
            check 'EqualsGetClass', CheckSeverity.ERROR
            check 'EqualsIncompatibleType', CheckSeverity.ERROR
            check 'EqualsUnsafeCast', CheckSeverity.ERROR
            check 'FutureReturnValueIgnored', CheckSeverity.ERROR
            check 'ImmutableEnumChecker', CheckSeverity.ERROR
            check 'InconsistentCapitalization', CheckSeverity.ERROR
            check 'JdkObsolete', CheckSeverity.ERROR
            check 'MissingOverride', CheckSeverity.ERROR
            check 'MutableConstantField', CheckSeverity.ERROR
            check 'NonAtomicVolatileUpdate', CheckSeverity.ERROR
            check 'ObjectToString', CheckSeverity.ERROR
            check 'OperatorPrecedence', CheckSeverity.ERROR
            check 'PrivateConstructorForUtilityClass', CheckSeverity.ERROR
            check 'ReferenceEquality', CheckSeverity.ERROR
            check 'StringSplitter', CheckSeverity.ERROR
            check 'ThreadPriorityCheck', CheckSeverity.ERROR
            check 'UndefinedEquals', CheckSeverity.ERROR
            check 'UnnecessaryParentheses', CheckSeverity.ERROR
            check 'UnsafeReflectiveConstructionCast', CheckSeverity.ERROR
            check 'UnsynchronizedOverridesSynchronized', CheckSeverity.ERROR
            check 'WaitNotInLoop', CheckSeverity.ERROR
            disable 'UnusedVariable' // Workaround for https://github.com/google/error-prone/issues/1250
        }
        options.errorprone.errorproneArgs = ['-Xep:Finally:OFF'] // Workaround for https://github.com/google/error-prone/issues/1257
        options.incremental = true
    }

    tasks.withType(Test).configureEach {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    check {
        dependsOn 'integTest'
    }

    checkstyle {
        toolVersion = '8.23'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [samedir: configFile.parent]
    }

    checkstyleMain {
        maxWarnings = 0
        source sourceSets.main.output.resourcesDir
    }

    checkstyleTest {
        maxWarnings = 0
        source sourceSets.test.output.resourcesDir
    }

    compileTestJava {
        options.errorprone {
            check 'ClassCanBeStatic', CheckSeverity.OFF
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    lombok {
        version = '1.18.4'
        sha256 = '39f3922deb679b1852af519eb227157ef2dd0a21eec3542c8ce1b45f2df39742'
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files(rootProject.file('config/pmd/pmd.xml'))
        ruleSets = []
    }

    spotless {
        java {
            googleJavaFormat()
            endWithNewline()
            removeUnusedImports()
            trimTrailingWhitespace()
        }
    }

    test {
        useJUnitPlatform {
            excludeTags 'integration'
        }
    }

    task integTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
        }
    }
}
