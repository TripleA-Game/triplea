plugins {
    id 'java'
}

apply from: 'gradle/scripts/yaml.gradle'

ext {
    schemasDir = file('config/triplea/schemas')
}

task validateYamls(group: 'verification', description: 'Validates YAML files.') {
    doLast {
        def lobbyServerYamlFile = file('lobby_server.yaml')
        validateYaml(lobbyServerYamlFile, file("$schemasDir/lobby_server.json"))

        def mapsYamlFile = file('triplea_maps.yaml')
        validateYaml(mapsYamlFile, file("$schemasDir/triplea_maps.json"))
        validateMapsYamlUris(mapsYamlFile)
    }
}

check {
    dependsOn 'validateYamls'
}

subprojects {
    ext {
        hamcrestVersion = '2.0.0.0'
        junitJupiterVersion = '5.1.0'
        mockitoVersion = '2.19.1'
    }

    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'java'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile "org.hamcrest:java-hamcrest:$hamcrestVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.mockito:mockito-junit-jupiter:$mockitoVersion"

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"

        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        testRuntime 'org.junit.platform:junit-platform-launcher:1.1.0'
        testRuntime 'org.slf4j:slf4j-nop:1.7.25'

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    }

    check {
        dependsOn 'integTest'
    }

    checkstyle {
        toolVersion = "8.8"
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
        configProperties = [samedir: configFile.parent]
    }

    checkstyleMain {
        maxWarnings = 0
        source sourceSets.main.output.resourcesDir
    }

    checkstyleTest {
        maxWarnings = 0
        source sourceSets.test.output.resourcesDir
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    test {
        useJUnitPlatform {
            excludeTags 'integration'
        }
    }

    task integTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
        }
    }
}
