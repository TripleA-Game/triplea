plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

description = 'TripleA Http Server'
mainClassName = 'org.triplea.server.http.spark.SparkServer'

dependencies {
    implementation project(':http-client')
    implementation 'com.sparkjava:spark-core:2.3'
    implementation 'com.google.code.gson:gson:2.7'
    
    testImplementation project(':test-common')
    testImplementation 'uk.co.datumedge:hamcrest-json:0.1'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task httpServerArchive(type: Zip, group: 'release', dependsOn: shadowJar) {
    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: httpServerArchive) {
    doLast {
        publishArtifacts(httpServerArchive.outputs.files)
    }
}
