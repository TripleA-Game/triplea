plugins {
    id 'de.undercouch.download' version '3.4.3'
}

description = 'TripleA core library containing code shared between headed and headless versions'

ext {
    remoteLibsDir = file('.remote-libs')
}

def remoteFile(url) {
    def file = file("$remoteLibsDir/${java.nio.file.Paths.get(new URI(url).path).fileName}")
    download {
        src url
        dest file
        overwrite false
    }
    files(file)
}

dependencies {
    compile 'com.github.openjson:openjson:1.0.10'
    compile 'com.googlecode.soundlibs:jlayer:1.0.1.4'
    compile 'com.sun.mail:javax.mail:1.6.1'
    compile 'commons-codec:commons-codec:1.11'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.5'
    compile 'org.apache.httpcomponents:httpmime:4.5.5'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.yaml:snakeyaml:1.19'
    compile 'com.yuvimasory:orange-extensions:1.3.0'
    compile 'commons-cli:commons-cli:1.4'
    compile remoteFile('https://github.com/kirill-grouchnikov/substance/raw/f894ef784a2ac20acf13e6ed2c7c26123399787b/drop/8.0.02/substance-8.0.02.jar')

    runtime remoteFile('https://github.com/kirill-grouchnikov/substance/raw/f894ef784a2ac20acf13e6ed2c7c26123399787b/drop/8.0.02/trident-1.5.00.jar')

    testCompile project(':test-common')
    testCompile 'org.sonatype.goodies:goodies-prefs:2.2.4'
    testCompile 'org.testfx:testfx-junit5:4.0.13-alpha'
    testCompile 'org.testfx:testfx-core:4.0.13-alpha'

    if (JavaVersion.current() == JavaVersion.VERSION_1_9) {
        testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-9+181'
    } else if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        testRuntimeOnly 'org.testfx:openjfx-monocle:8u76-b04'
    }
}

checkstyleMain {
    maxWarnings = checkstyleMainMaxWarnings.toInteger()
}

checkstyleTest {
    maxWarnings = checkstyleTestMaxWarnings.toInteger()
}

task cleanRemoteLibs(
        type: Delete,
        group: LifecycleBasePlugin.BUILD_GROUP,
        description: 'Deletes the remote libraries directory.') {
    delete remoteLibsDir
}
