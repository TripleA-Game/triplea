plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'de.undercouch.download' version '3.4.3'
}

archivesBaseName = "$group-$name"
description = 'TripleA Smoke Testing Application'
mainClassName = 'org.triplea.test.smoke.ClientConnect'

dependencies {
    implementation project(':game-core')
    implementation project(':http-clients')
    testImplementation project(':test-common')
}

shadowJar {
    archiveClassifier.set ''
}

tasks.test.dependsOn("downloadXmls")

task downloadXmls {
    doLast {
        mkdir "$projectDir/src/test/resources/map-xmls/"
        file('map-list.txt').eachLine { path ->
            if (!path.startsWith('#')) {
                download {
                    src "$path"
                    dest "$projectDir/src/test/resources/map-xmls/"
                    overwrite false
                }
            }
        }
    }
}

clean.doFirst {
    delete "$projectDir/src/test/resources/map-xmls/"
}
