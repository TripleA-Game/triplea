plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

archivesBaseName = "$group-$name"
description = 'TripleA Lobby'
mainClassName = 'org.triplea.lobby.server.LobbyRunner'
version = getEngineVersion()

dependencies {
    compile project(':game-core')
    compile 'org.mindrot:jbcrypt:0.4'

    runtime "org.postgresql:postgresql:$postgresqlVersion"

    testCompile project(':test-common')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task lobbyArchive(type: Zip, group: 'release', dependsOn: shadowJar) {
    from('config') {
        into 'config'
    }
    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: lobbyArchive) {
    doLast {
        publishArtifacts(lobbyArchive.outputs.files)
    }
}
