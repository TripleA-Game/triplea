plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.google.cloud.tools.jib' version '1.0.2'
}

archivesBaseName = "$group-$name"
description = 'TripleA Lobby'
mainClassName = 'org.triplea.lobby.server.LobbyRunner'

dependencies {
    implementation project(':java-extras')
    implementation project(':game-core')
    implementation project(':lobby-db-dao')
    implementation "org.mindrot:jbcrypt:$jbcryptVersion"
    implementation 'org.jdbi:jdbi3-core:3.8.2'
    implementation 'org.jdbi:jdbi3-sqlobject:3.8.2'

    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.21'

    testImplementation project(':test-common')
    testImplementation 'com.github.database-rider:rider-junit5:1.5.2'
    testImplementation 'uk.co.datumedge:hamcrest-json:0.2'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

task copyShadow(dependsOn: shadowJar) {
    doLast {
        copy {
            from shadowJar
            into "../infrastructure/ansible/roles/lobby/files/"
        }
    }
}
