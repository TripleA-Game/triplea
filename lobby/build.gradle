plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.google.cloud.tools.jib' version '1.0.2'
}

archivesBaseName = "$group-$name"
description = 'TripleA Lobby'
mainClassName = 'org.triplea.lobby.server.LobbyRunner'

dependencies {
    implementation "org.jdbi:jdbi3-core:$jdbiVersion"
    implementation "org.jdbi:jdbi3-sqlobject:$jdbiVersion"
    implementation "org.mindrot:jbcrypt:$jbcryptVersion"
    implementation project(":game-core")
    implementation project(":http-clients")
    implementation project(":java-extras")
    implementation project(":lobby-db-dao")
    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jSimpleVersion"
    testImplementation "com.github.database-rider:rider-junit5:$databaseRiderVersion"
    testImplementation "uk.co.datumedge:hamcrest-json:$hamcrestJsonVersion"
    testImplementation project(":test-common")
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

task copyShadow(dependsOn: shadowJar) {
    doLast {
        copy {
            from shadowJar
            into "../infrastructure/ansible/roles/lobby/files/"
        }
    }
}
