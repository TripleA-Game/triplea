plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

description = 'TripleA Lobby'
mainClassName = 'games.strategy.engine.lobby.server.LobbyRunner'
version = project.hasProperty('lobbyVersion') ? project.lobbyVersion : 'dev'

jar {
    manifest {
        attributes 'Main-Class': mainClassName, 'TripleA-Version': version
    }
}

dependencies {
    compile project(':game-core')
    compile 'org.mindrot:jbcrypt:0.4'

    runtime "org.postgresql:postgresql:$postgresqlVersion"

    testCompile project(':test-common')
}

shadowJar {
    destinationDir = libsDir
    baseName = 'triplea-lobby'
    classifier = 'all'
}

task lobbyServer(type: Zip, group: 'release', dependsOn: shadowJar) {
    baseName = 'triplea'
    classifier = 'server'
    from('config') {
        into('config')
    }
    from(shadowJar.outputs) {
        into('bin')
    }
}

task release(group: 'release', dependsOn: lobbyServer) {
    doLast {
        publishArtifacts([
            file("$distsDir/triplea-$version-server.zip")
        ])
    }
}
